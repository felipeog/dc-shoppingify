app wasp {
  wasp: {
    version: "^0.11.7"
  },
  title: "Shoppingify",
  auth: {
    userEntity: User,
    methods: {
      usernameAndPassword: {}
    },
    onAuthFailedRedirectTo: "/login"
  }
}

// start: entities
entity User {=psl
  id        Int         @id @default(autoincrement())
  username  String      @unique
  password  String
  lists     ItemsList[]
psl=}

entity ItemsList {=psl
  id      Int         @id @default(autoincrement())
  name    String      @unique
  items   ListItem[]
  state   String
  user    User        @relation(fields: [userId], references: [id])
  userId  Int
psl=}

entity ListItem {=psl
  id          Int       @id @default(autoincrement())
  name        String    @unique
  isDone      Boolean
  item        Item      @relation(fields: [itemId], references: [id])
  itemId      Int
  itemsList   ItemsList @relation(fields: [itemsListId], references: [id])
  itemsListId Int
psl=}

entity Item {=psl
  id          Int         @id @default(autoincrement())
  name        String      @unique
  note        String
  image       String
  listItems   ListItem[]
  category    Category    @relation(fields: [categoryId], references: [id])
  categoryId  Int
psl=}

entity Category {=psl
  id    Int     @id @default(autoincrement())
  name  String  @unique
  items Item[]
psl=}
// end: entities

// start: queries
query getCategories {
  fn: import { getCategories } from "@server/queries.js",
  entities: [Category]
}
// end: queries

// start: actions
action createCategory {
  fn: import { createCategory } from "@server/actions.js",
  entities: [Category]
}

action deleteCategory {
  fn: import { deleteCategory } from "@server/actions.js",
  entities: [Category]
}
// end: actions

// start: routes
route RootRoute {
  path: "/",
  to: MainPage
}
page MainPage {
  authRequired: true,
  component: import { MainPage } from "@client/MainPage.tsx"
}

route SignupRoute {
  path: "/signup",
  to: SignupPage
}
page SignupPage {
  component: import { SignupPage } from "@client/SignupPage.tsx"
}

route LoginRoute {
  path: "/login",
  to: LoginPage
}
page LoginPage {
  component: import { LoginPage } from "@client/LoginPage.tsx"
}

route DebugItemsListRoute {
  path: "/debug/items-list",
  to: DebugItemsListPage
}
page DebugItemsListPage {
  authRequired: true,
  component: import { DebugItemsListPage } from "@client/debug/DebugItemsListPage.tsx"
}

route DebugListItemRoute {
  path: "/debug/list-item",
  to: DebugListItemPage
}
page DebugListItemPage {
  authRequired: true,
  component: import { DebugListItemPage } from "@client/debug/DebugListItemPage.tsx"
}

route DebugItemRoute {
  path: "/debug/item",
  to: DebugItemPage
}
page DebugItemPage {
  authRequired: true,
  component: import { DebugItemPage } from "@client/debug/DebugItemPage.tsx"
}

route DebugCategoryRoute {
  path: "/debug/category",
  to: DebugCategoryPage
}
page DebugCategoryPage {
  authRequired: true,
  component: import { DebugCategoryPage } from "@client/debug/DebugCategoryPage.tsx"
}
// end: routes
